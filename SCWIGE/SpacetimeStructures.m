(* Wolfram Language package *)
        
safeUVs = {{1, 36/25}, {1, 100/169}, {1, 256/289}, {1, 1600/841}, {1, 
    3136/2809}, {1, 4356/4225}, {1, 5184/7225}, {1, 6084/7921}, {1, 
    9216/5329}, {1, 14400/11881}, {9/16, 25/16}, {9/16, 1369/
    2704}, {16/9, 1369/1521}, {16/9, 8281/12321}, {25/16, 9/16}, {25/
    36, 25/36}, {36/25, 1}, {64/81, 7225/13689}, {81/64, 7225/
    10816}, {100/169, 1}, {169/100, 169/100}, {169/196, 225/
    196}, {169/225, 196/225}, {196/169, 225/169}, {196/225, 169/
    225}, {196/361, 10201/9025}, {225/169, 196/169}, {225/196, 169/
    196}, {256/289, 1}, {289/256, 289/256}, {289/441, 5776/
    11025}, {361/625, 4356/7225}, {400/441, 841/441}, {441/289, 5776/
    7225}, {625/361, 4624/9025}, {625/841, 1296/841}, {625/1156, 1521/
    1156}, {841/441, 400/441}, {841/1225, 2304/1225}, {841/1600, 841/
    1600}, {1296/841, 625/841}, {1369/1521, 16/9}, {1369/2704, 9/
    16}, {1521/1156, 625/1156}, {1521/1369, 2704/1369}, {1521/1681, 
    2500/1681}, {1521/2500, 1681/2500}, {1600/841, 1}, {1681/1521, 
    2500/1521}, {1681/2500, 1521/2500}, {1681/2601, 3364/2601}, {2304/
    1225, 841/1225}, {2304/3025, 5329/3025}, {2500/1521, 1681/
    1521}, {2500/1681, 1521/1681}, {2500/4761, 5329/4761}, {2601/2704,
     2809/2704}, {2601/2809, 2704/2809}, {2704/1369, 1521/
    1369}, {2704/2601, 2809/2601}, {2704/2809, 2601/2809}, {2704/5329,
     5625/5329}, {2809/2601, 2704/2601}, {2809/2704, 2601/
    2704}, {2809/3136, 2809/3136}, {3136/2809, 1}, {3136/3721, 5625/
    3721}, {3136/5625, 3721/5625}, {3249/4225, 4624/4225}, {3249/4624,
     4225/4624}, {3364/2601, 1681/2601}, {3600/5329, 8281/
    5329}, {3721/3136, 5625/3136}, {3721/5476, 7569/5476}, {3721/5625,
     3136/5625}, {3721/6400, 11881/6400}, {4225/3249, 4624/
    3249}, {4225/4356, 4225/4356}, {4225/4624, 3249/4624}, {4225/5184,
     9409/5184}, {4225/5776, 7569/5776}, {4225/7569, 5776/
    7569}, {4225/7569, 7744/7569}, {4225/7744, 7569/7744}, {4356/4225,
     1}, {4356/7225, 361/625}, {4489/7225, 13456/7225}, {4624/3249, 
    4225/3249}, {4624/4225, 3249/4225}, {4624/5625, 5929/5625}, {4624/
    5929, 5625/5929}, {4624/7569, 9025/7569}, {4624/9025, 625/
    361}, {4624/9025, 7569/9025}, {5184/7225, 1}, {5329/3025, 2304/
    3025}, {5329/4761, 2500/4761}, {5329/9216, 5329/9216}, {5625/3136,
     3721/3136}, {5625/3721, 3136/3721}, {5625/4624, 5929/
    4624}, {5625/5329, 2704/5329}, {5625/5929, 4624/5929}, {5625/7396,
     9409/7396}, {5625/9409, 7396/9409}, {5776/4225, 7569/
    4225}, {5776/7225, 441/289}, {5776/7569, 4225/7569}, {5776/11025, 
    289/441}, {5929/4624, 5625/4624}, {5929/5625, 4624/5625}, {6084/
    7921, 1}, {6084/9025, 9409/9025}, {6084/9409, 9025/9409}, {6084/
    10201, 13225/10201}, {7225/5184, 7225/5184}, {7225/5776, 11025/
    5776}, {7225/8649, 13456/8649}, {7225/10816, 81/64}, {7225/13456, 
    8649/13456}, {7225/13689, 64/81}, {7396/5625, 9409/5625}, {7396/
    9409, 5625/9409}, {7569/4225, 5776/4225}, {7569/4225, 7744/
    4225}, {7569/4624, 9025/4624}, {7569/5476, 3721/5476}, {7569/5776,
     4225/5776}, {7569/7744, 4225/7744}, {7569/9025, 4624/
    9025}, {7744/4225, 7569/4225}, {7744/7569, 4225/7569}, {7921/6084,
     7921/6084}, {7921/9801, 10000/9801}, {7921/10000, 9801/
    10000}, {8100/9409, 14161/9409}, {8100/14161, 9409/14161}, {8281/
    5329, 3600/5329}, {8281/12321, 16/9}, {8281/13225, 13456/
    13225}, {8281/13456, 13225/13456}, {8464/12321, 14161/
    12321}, {8464/14161, 12321/14161}, {8649/7225, 13456/7225}, {8649/
    13456, 7225/13456}, {9025/4624, 7569/4624}, {9025/6084, 9409/
    6084}, {9025/7569, 4624/7569}, {9025/9409, 6084/9409}, {9216/5329,
     1}, {9409/5184, 4225/5184}, {9409/5625, 7396/5625}, {9409/6084, 
    9025/6084}, {9409/7396, 5625/7396}, {9409/8100, 14161/
    8100}, {9409/9025, 6084/9025}, {9409/14161, 8100/14161}, {9801/
    7921, 10000/7921}, {9801/10000, 7921/10000}, {10000/7921, 9801/
    7921}, {10000/9801, 7921/9801}, {10201/9025, 196/361}, {10816/
    7225, 13689/7225}, {11025/5776, 7225/5776}, {11236/12769, 14161/
    12769}, {11236/14161, 12769/14161}, {11881/6400, 3721/
    6400}, {11881/14400, 11881/14400}, {12321/8464, 14161/
    8464}, {12321/14161, 8464/14161}, {12769/11236, 14161/
    11236}, {12769/14161, 11236/14161}, {13225/8281, 13456/
    8281}, {13225/10201, 6084/10201}, {13225/13456, 8281/
    13456}, {13456/7225, 4489/7225}, {13456/7225, 8649/7225}, {13456/
    8281, 13225/8281}, {13456/8649, 7225/8649}, {13456/13225, 8281/
    13225}, {13689/7225, 10816/7225}, {14161/8100, 9409/8100}, {14161/
    8464, 12321/8464}, {14161/9409, 8100/9409}, {14161/11236, 12769/
    11236}, {14161/12321, 8464/12321}, {14161/12769, 11236/
    12769}, {14400/11881, 1}};

uvpt = {x[1, i_] :> -Boole[i == 4], x[2, _] :> 0, 
   x[3, i_] :> Boole[i == 4], 
   x[4, i_] :> 
    Which[i == 3, -(
      Sqrt[-u^2 - (-1 + v)^2 + 2 u (1 + v)]/(-1 + 2 u + 2 v)), 
     i == 4, (-u + v)/(-1 + 2 u + 2 v), True, 0]};
sct[x_, b_] := (x - b x . Components[\[Eta]Lower] . x)/(1 - 
     2 (b . DiagonalMatrix[{-1, 1, 1, 1}] . x) + 
     (b . Components[\[Eta]Lower] . b) (x . Components[\[Eta]Lower] . x));
sctuvpt[z_] = 
  Simplify@
   Flatten@Table[
     x[i, j] -> 
      sct[Table[x[i, kk] /. uvpt, {kk, 4}], {0, z, 0, 0}][[j]], {i, 
      4}, {j, 4}];
uvAssumptions = (1/2 < u < 2 && 1/2 < v < 2);

SymbolicSpacetimeRelations[largebasis_] := If[# === {}, {}, 
   FullSimplify[
    RowReduce[#, 
     ZeroTest -> (Function[expr, Simplify[expr, uvAssumptions] === 0])], 
    uvAssumptions]] &@
 FullSimplify[
  NullSpace[
   Transpose[
     ArrayFlatten[
      Table[Flatten[
        CanonicallyOrderedComponents[largebasis[[i]]]], {i, 
        Length[largebasis]}]]] /. sctuvpt[2]], uvAssumptions];

SpacetimeRelations[structs_] := 
  If[Length[structs] > 5 && 
    First@Cases[structs, s_SpacetimeStructure :> Length[s[[1]]], All] == 4, 
   relations[structs, 188, 6], SymbolicSpacetimeRelations[structs]];
    
spacetimePtData[structs_, len_] := 
 spacetimePtData[structs, len] = 
  With[{structComps = 
     Simplify[
      ArrayFlatten[
        Flatten@*CanonicallyOrderedComponents /@ structs] /. 
       sctuvpt[2], uvAssumptions]},
   With[{idxs = Sort[Length[structs] + 1 - IndependentSet[Reverse@structComps, "Rules" -> Thread[{u, v} -> safeUVs[[-2]]], "Indices" -> True]]}, {idxs, 
     ResourceFunction["MonitorProgress"][
      Table[Simplify@
        Quiet@Check[{safeUVs[[ii]], 
           Table[LinearSolve[
             Transpose[structComps[[idxs]]] /. {u -> safeUVs[[ii, 1]],
                v -> safeUVs[[ii, 2]]}, 
             structComps[[reli]] /. {u -> safeUVs[[ii, 1]], 
               v -> safeUVs[[ii, 2]]}], {reli, 
             Complement[Range@Length[structs], idxs]}]}, 
          Nothing], {ii, len}], 
      "Label" -> "Generating spacetime structure relation data",
		"CurrentDisplayFunction" -> None]}
    ]
   ];

KinematicPrefactor[{\[CapitalDelta]1_, \[CapitalDelta]2_}, {{l1_, lb1_}, {l2_, lb2_}}] := 1/XXSquared[1, 2]^(\[CapitalDelta]1 + l1 + lb1);
KinematicPrefactor[deltas_, spins_] /; Length[deltas] == 3 := With[{kappas = deltas + Total /@ spins},
   XXSquared[1,2]^((kappas[[3]] - kappas[[1]] - kappas[[2]])/2) XXSquared[1,3]^((-kappas[[3]] - kappas[[1]] + kappas[[2]])/2) XXSquared[2,3]^((-kappas[[3]] + kappas[[1]] - kappas[[2]])/2)
];
KinematicPrefactor[deltas_, spins_] /; Length[deltas] == 4 := With[{kappas = deltas + Total /@ spins},
   (XXSquared[2, 4]/XXSquared[1, 4])^((kappas[[1]] - kappas[[2]])/2) (XXSquared[1, 4]/XXSquared[1, 3])^((kappas[[3]] - kappas[[4]])/2) XXSquared[1, 2]^(-(kappas[[1]] + kappas[[2]])/2) XXSquared[3, 4]^(-(kappas[[3]] + kappas[[4]])/2)
];

MyInactive /: MakeBoxes[MyInactive[StructureI][i_, j_], TraditionalForm] :=
   SuperscriptBox[OverscriptBox["I", "^"], 
   RowBox@{ToString[i], ",", ToString[j]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureI][i_, j_, k_, l_], TraditionalForm] := 
  SubsuperscriptBox[OverscriptBox["I", "^"], 
   RowBox@{ToString[k], ",", ToString[l]}, 
   RowBox@{ToString[i], ",", ToString[j]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureJ][k_, i_, j_], TraditionalForm] := 
  SubsuperscriptBox[OverscriptBox["J", "^"], 
   RowBox@{ToString[i], ",", ToString[j]}, RowBox@{ToString[k]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureK][i_, j_, k_], TraditionalForm] := 
  SubsuperscriptBox[OverscriptBox["K", "^"], 
   RowBox@{ToString[k]}, RowBox@{ToString[i], ",", ToString[j]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureKBar][i_, j_, k_], TraditionalForm] := 
  SubsuperscriptBox[
   OverscriptBox[OverscriptBox["K", "_"], "^"], 
   RowBox@{ToString[k]}, RowBox@{ToString[i], ",", ToString[j]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureL][i_, j_, k_, l_], TraditionalForm] := 
  SubsuperscriptBox[OverscriptBox["L", "^"], 
   RowBox@{ToString[j], ",", ToString[k], ",", ToString[l]}, 
   RowBox@{ToString[i]}];
MyInactive /: 
  MakeBoxes[MyInactive[StructureLBar][i_, j_, k_, l_], 
   TraditionalForm] := 
  SubsuperscriptBox[
    OverscriptBox[OverscriptBox["L", "_"], "^"], 
   RowBox@{ToString[j], ",", ToString[k], ",", ToString[l]}, 
   RowBox@{ToString[i]}];
       
StructureI[i_, j_] := SpinorX[i, j];
StructureI[i_, j_, k_, l_] /; i === j := StructureJ[i, k, l];
StructureI[i_, j_, k_, l_] /; i =!= j := 1/(2 XXSquared[k, 
      l]) ((XXSquared[i, k] SpinorX[j, l] - 
        XXSquared[i, l] SpinorX[j, k]) + (XXSquared[j, k] SpinorX[i, 
          l] - XXSquared[j, l] SpinorX[i, k]) - 
      XXSquared[i, j] SpinorX[k, l] - XXSquared[k, l] SpinorX[i, j] - 
      (2 I / SignatureFactor[]) SpinorX[{i, k}, {l, j}, {l, k}]);

StructureJ[k_, i_, j_] := (XXSquared[i, k] XXSquared[j, k])/
   XXSquared[i, 
    j] (SpinorX[i, k]/XXSquared[i, k] - SpinorX[j, k]/XXSquared[j, k]);
StructureK[i_, j_, k_] := 
  1/2 Sqrt[XXSquared[i, j]]/
   Sqrt[XXSquared[i, k] XXSquared[j, 
      k]] ((XXSquared[i, k] + XXSquared[j, k] - 
        XXSquared[i, j]) \[Epsilon]Lower - 
     4 Contract[
       TensorProduct[XX[i, k], 
        XX[j, k], \[Sigma]CommLower], {{1, 3}, {2, 4}}]);
StructureKBar[i_, j_, k_] := 
  1/2 Sqrt[XXSquared[i, j]]/
   Sqrt[XXSquared[i, k] XXSquared[j, 
      k]] ((XXSquared[i, k] + XXSquared[j, k] - 
        XXSquared[i, j]) \[Epsilon]LowerDot - 
     4 Contract[
       TensorProduct[XX[i, k], 
        XX[j, k], \[Sigma]CommLowerDot], {{1, 3}, {2, 4}}]);
StructureL[i_, j_, k_, l_] := 
  2/Sqrt[XXSquared[j, k] XXSquared[k, l] XXSquared[l, 
      j]] (XXSquared[i, j] Contract[
       TensorProduct[XX[k, l], 
        XX[i, l], \[Sigma]CommLower], {{1, 3}, {2, 4}}] + 
     XXSquared[i, k] Contract[
       TensorProduct[XX[l, j], 
        XX[i, j], \[Sigma]CommLower], {{1, 3}, {2, 4}}] + 
     XXSquared[i, l] Contract[
       TensorProduct[XX[j, k], 
        XX[i, k], \[Sigma]CommLower], {{1, 3}, {2, 4}}]);
StructureLBar[i_, j_, k_, l_] := 
  2/Sqrt[XXSquared[j, k] XXSquared[k, l] XXSquared[l, 
      j]] (XXSquared[i, j] Contract[
       TensorProduct[XX[k, l], 
        XX[i, l], \[Sigma]CommLowerDot], {{1, 3}, {2, 4}}] + 
     XXSquared[i, k] Contract[
       TensorProduct[XX[l, j], 
        XX[i, j], \[Sigma]CommLowerDot], {{1, 3}, {2, 4}}] + 
     XXSquared[i, l] Contract[
       TensorProduct[XX[j, k], 
        XX[i, k], \[Sigma]CommLowerDot], {{1, 3}, {2, 4}}]);
        
numSTStructures[ls_] := 
 Which[Length[ls] == 2, Boole[ls[[2]] == Reverse[ls[[1]]]], Length[ls] == 3, 
  With[{js = 
     Tuples[Range[Abs[#[[1]] - #[[2]]], #[[1]] + #[[2]]] & /@ ls]},
   Count[
    js, {x_?NumericQ, y_, z_} /; 
     Total[Sort[{x, y, z}][[;; 2]]] >= Max[{x, y, z}] && 
      IntegerQ[x + y + z]]
   ], Length[ls] == 4, 
  Length@Select[Tuples[Range[-#, #] & /@ Flatten[ls]], 
    Total[#[[{1, 3, 5, 7}]]] == Total[#[[{2, 4, 6, 8}]]] &]]

validStruct[MyInactive[StructureI][i_, j_]] := i != j;
validStruct[
   MyInactive[StructureI][i_, j_, k_, l_]] := (k > l && 
    Length[DeleteDuplicates[{i, j, k, l}]] == 4);
validStruct[
   MyInactive[StructureJ][i_, j_, k_]] := (j < k && 
    Length[DeleteDuplicates[{i, j, k}]] == 3);
validStruct[
   MyInactive[StructureK][i_, j_, k_]] := (i < j && 
    Length[DeleteDuplicates[{i, j, k}]] == 3);
validStruct[
   MyInactive[StructureKBar][i_, j_, k_]] := (i < j && 
    Length[DeleteDuplicates[{i, j, k}]] == 3);
validStruct[
   MyInactive[StructureL][i_, j_, k_, l_]] := (j < k < l && 
    Length[DeleteDuplicates[{i, j, k, l}]] == 4);
validStruct[
   MyInactive[StructureLBar][i_, j_, k_, l_]] := (j < k < l && 
    Length[DeleteDuplicates[{i, j, k, l}]] == 4);

charges[MyInactive[StructureI][i_, j_], n_] := 
  SparseArray[{{i, 2} -> 1/2, {j, 1} -> 1/2}, {n, 2}];
charges[MyInactive[StructureI][i_, j_, k_, l_], n_] := 
 SparseArray[{{i, 2} -> 1/2, {j, 1} -> 1/2}, {n, 2}]; 
charges[MyInactive[StructureJ][i_, j_, k_], n_] := 
 SparseArray[{{i, 2} -> 1/2, {i, 1} -> 1/2}, {n, 2}]; 
charges[MyInactive[StructureK][i_, j_, k_], n_] := 
 SparseArray[{{i, 1} -> 1/2, {j, 1} -> 1/2}, {n, 2}]; 
charges[MyInactive[StructureKBar][i_, j_, k_], n_] := 
 SparseArray[{{i, 2} -> 1/2, {j, 2} -> 1/2}, {n, 2}]; 
charges[MyInactive[StructureL][i_, j_, k_, l_], n_] := 
 SparseArray[{{i, 1} -> 1}, {n, 2}]; 
charges[MyInactive[StructureLBar][i_, j_, k_, l_], n_] := 
 SparseArray[{{i, 2} -> 1}, {n, 2}];

structTypes[n_] := 
  Which[n == 2, {MyInactive[StructureI][1, 2], 
    MyInactive[StructureI][2, 1]}, n == 3, 
   Select[Flatten@
     Join[MyInactive[StructureI] @@@ Tuples[Range[3], 2], 
      MyInactive[StructureJ] @@@ Permutations[Range[3]], 
      MyInactive[StructureK] @@@ Permutations[Range[3]], 
      MyInactive[StructureKBar] @@@ Permutations[Range[3]]], 
    validStruct], n == 4, 
   Select[Flatten@
     Join[
      MyInactive[StructureI] @@@ Tuples[Range[4], 2], 
      MyInactive[StructureI] @@@ Permutations[Range[4]], 
      MyInactive[StructureJ] @@@ Tuples[Range[4], 3], 
      MyInactive[StructureK] @@@ Tuples[Range[4], 3], 
      MyInactive[StructureKBar] @@@ Tuples[Range[4], 3], 
      MyInactive[StructureL] @@@ Permutations[Range[4]], 
      MyInactive[StructureLBar] @@@ Permutations[Range[4]]], 
    validStruct]];

structBasis[n_] := Normal[charges[#, n]] -> # & /@ structTypes[n];

maxMultiplier[basisElem_, target_] := 
  Floor@Min[Part[target, Sequence @@ #[[1]]]/#[[2]] & /@ 
    Select[ArrayRules[basisElem], TrueQ[#[[2]] > 0] &]];
sumsToProducts[{}, target_] := If[Total@Flatten[target] === 0, {1}, {}];
sumsToProducts[basis_, target_] := 
  Flatten@Table[
    Thread[TensorProduct[TensorProduct[Sequence @@ Table[basis[[1,2]], n]], sumsToProducts[Rest[basis], target - n basis[[1, 1]]]]], {n,
     maxMultiplier[basis[[1, 1]], target], 0, -1}];
     
structToIndexRules = {
   MyInactive[StructureI][i_, j_, ___] :> {{j, Spinor}, {i, DottedSpinor}},
   MyInactive[StructureJ][i_, j_, k_] :> {{i, Spinor}, {i, DottedSpinor}},
   MyInactive[StructureK][i_, j_, k_] :> {{i, Spinor}, {j, Spinor}},
   MyInactive[StructureKBar][i_, j_, k_] :> {{i, DottedSpinor}, {j, DottedSpinor}},
   MyInactive[StructureL][i_, j_, k_, l_] :> {{i, Spinor}, {i, Spinor}},
   MyInactive[StructureLBar][i_, j_, k_, l_] :> {{i, DottedSpinor}, {i, DottedSpinor}},
   TensorProduct -> Join
};

constructStructure[expr_, perm_] := With[{inds = (expr /. structToIndexRules)},
	With[{unsym = TensorTranspose[CanonicallyOrderedComponents[expr /. MyInactive -> Identity], Ordering[inds[[;; , 2]]]], syms = Select[Permutations[Range@Length[inds]], inds[[#]] === inds &]},
	   If[syms == {}, unsym,
		(1/Length[syms]) TensorTranspose[Sum[TensorTranspose[unsym, p], {p, syms}], perm]
	   ]
	]
]

SpacetimeStructureExpressions::num = "Only 2-, 3-, and 4-point structures are supported.";
Options[SpacetimeStructureExpressions] = {"Overcomplete" -> False};
SpacetimeStructureExpressions[ls_, opt : OptionsPattern[]] := SpacetimeStructureExpressions[ls, opt] = If[!(2 <= Length[ls] <= 4),
   Message[SpacetimeStructureExpressions::num],
   If[TrueQ[OptionValue["Overcomplete"]],
   	With[{s2p = sumsToProducts[structBasis[Length[ls]], ls], cinds = Flatten[Table[{i, If[j == 1, Spinor, DottedSpinor]}, {i, Length[ls]}, {j, 2}, {k, 2 ls[[i, j]]}], 2]},
        {#, FindPermutation[If[Total[Flatten[ls]] == 0, {}, # /. structToIndexRules], cinds]}& /@ s2p
   	],
   	With[{structs = SpacetimeStructureExpressions[ls, "Overcomplete" -> True]},
	   	With[{comps = constructStructure @@@ structs},
	   		structs[[IndependentSet[comps, "Rules" -> (sctuvpt[2] /. Thread[{u, v} -> safeUVs[[2]]]), "MaxIndependent" -> numSTStructures[ls], "Indices" -> True]]]
	   	]
   	]
   ]
];

SpacetimeStructures[dims_, ls_, derivs_, derivtype_, perm_] := Table[Tensor[{{SpacetimeStructure[dims, ls, derivs, derivtype, perm, i],
   Sequence @@ Flatten[Table[
      {Table[{Lowered[Spinor], Lowered[DottedSpinor]}, Count[derivs, j]], Table[Lowered[Spinor], 2 ls[[j, 1]]], Table[Lowered[DottedSpinor], 2 ls[[j, 2]]]},
      {j, Length[dims]}]]}}], {i, numSTStructures[ls]}];
      
BuildTensor[{SpacetimeStructure[dims_, ls_, {}, derivtype_, perm_, i_], idxs___}] := With[{comps = Explicit[KinematicPrefactor[dims, ls]] (constructStructure @@ SpacetimeStructureExpressions[ls][[i]])},
 	If[Length[{idxs}] == 0, 
 	   comps /. x[ii_, j_] :> x[perm[[ii]], j],
 	   SparseArray[ArrayRules[comps] /. x[ii_, j_] :> x[perm[[ii]], j]]
 	]    
];

BuildTensor[{SpacetimeStructure[dims_, ls_, 
      derivs_, derivtype_, perm_, i_], idxs___}] /; derivs =!= {} := 
  With[{bd = 
     SpacetimeStructures[dims, ls, {}, "\[PartialD]", perm][[i]]},
   With[{dsis = 
      Table[2 Length[derivs] + 
        Join @@ (Range[#1 + 1, #2] & @@@ 
           Partition[Accumulate[2 Flatten[ls[[;; ii]]]], 2]), {ii, 
        Length[dims] - 1}],
     sis = 
      Table[2 Length[derivs] + 
        Join @@ (Range[#1 + 1, #2] & @@@ 
           Partition[Accumulate[Prepend[2 Flatten[ls[[;; ii]]], 0]], 
            2]), {ii, Length[dims] - 1}], 
     pd = perm[[#]] & /@ derivs},
    TensorTranspose[
     CanonicallyOrderedComponents@Fold[TensorPermute, Switch[derivtype,
        "\[PartialD]", 
        Fold[TensorSpinorDerivative, bd, ReverseSort[pd]],
        "u", 
        TensorProduct[
         Fold[TensorSpinorDerivative, u[perm], ReverseSort[pd]], bd],
        "v", 
        TensorProduct[
         Fold[TensorSpinorDerivative, v[perm], ReverseSort[pd]], bd]
        ], 
       Table[PermutationList[
         InversePermutation@
          PermutationPower[
           Cycles[{Join[
              Range[2 Count[derivs, x_ /; x <= (Length[dims] - ii)] + 
                1, 2 Count[derivs, 
                 x_ /; x <= (Length[dims] + 1 - ii)], 2], 
              sis[[Length[dims] - ii]]], 
             Join[Range[
               2 Count[derivs, x_ /; x <= (Length[dims] - ii)] + 2, 
               2 Count[derivs, x_ /; x <= (Length[dims] + 1 - ii)], 
               2], dsis[[Length[dims] - ii]]]}], 
           Count[derivs, Length[dims] + 1 - ii]], 
         Length@Indices[bd] + 2 Length[pd]], {ii, Length[dims] - 1}]],
      Ordering@{idxs}]
    ]
   ];